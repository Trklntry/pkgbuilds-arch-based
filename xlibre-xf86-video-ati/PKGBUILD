# Maintainer: artist for Xlibre

pkgname=xlibre-xf86-video-ati
_pkgname=xf86-video-ati
_pkgname2=xfree-xf86-video-ati
pkgver=22.0.0.1
pkgrel=1
pkgdesc="XLibre fork of X.Org ati video driver"
arch=('x86_64' 'aarch64')
url="https://github.com/X11Libre"
license=('MIT')
depends=('mesa' 'libpciaccess' 'libdrm' 'glibc')
makedepends=('xlibre-xserver' 'xlibre-xserver-common' 'xlibre-xserver-devel' 'X-ABI-VIDEODRV_VERSION=28.0'
  'xorgproto' 'autoconf' 'automake' 'binutils' 'fakeroot' 'findutils'
  'gawk' 'gcc' 'gettext' 'gzip' 'libtool' 'm4' 'make' 'patch' 'pkgconf')
conflicts=(${_pkgname} 'X-ABI-VIDEODRV_VERSION<28' 'X-ABI-VIDEODRV_VERSION>=29')
provides=(${_pkgname})
groups=('xlibre-drivers')
source=("${url}/${_pkgname}/archive/refs/tags/${_pkgname2}-${pkgver}.tar.gz")
options=('!debug')

build() {
  case "$CARCH" in
    "x86_64")
      CFLAGS+=""
      ;;
    "aarch64")
      CFLAGS=" -march=aarch64"
      ;;
    *)
      CFLAGS=" -march=native"
      ;;
  esac
  CFLAGS+=" -mtune=generic -O2 -pipe -fno-plt -fexceptions -Wp,-D_FORTIFY_SOURCE=3 -Wformat -Werror=format-security"
  CFLAGS+=" -fstack-clash-protection -fno-omit-frame-pointer -mno-omit-leaf-frame-pointer -flto=auto"
  LDFLAGS=" -Wl,-O1 -Wl,--sort-common -Wl,--as-needed -Wl,-z,relro -Wl,-z,now -Wl,-z,pack-relative-relocs -flto=auto"

  cd ${_pkgname}-${_pkgname2}-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

check() {
  cd ${_pkgname}-${_pkgname2}-${pkgver}
  make check
}

package() {
  cd ${_pkgname}-${_pkgname2}-${pkgver}
  make "DESTDIR=${pkgdir}" install

  install -Dm644 "${srcdir}"/${_pkgname}-${_pkgname2}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

sha256sums=('e672335e6c3daaa7869ae59724197a7ae2b43c805e6528395f047302862f6541')

